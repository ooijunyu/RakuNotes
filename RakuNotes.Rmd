---
title: "RakuNotes"
output: html_document
knit: (function(input_file, encoding) {
  out_dir <- 'docs';
  rmarkdown::render(input_file,
 encoding=encoding,
 output_file=file.path(dirname(input_file), out_dir, 'index.html'))})
---
```{r echo = FALSE}
knitr::opts_chunk$set(eval=FALSE)
```

# Chapter 9 Arrays and List

The values in a list or in an array are called *elements* or *items*.

1. List: **Ordered** and **Immutable**
2. Arrays: **Variables** and generally **Mutable**

## List
```{r eval=FALSE}
> 3,4,5
(3 4 5)
> say (3, 4, 5).WHAT
(List)
> say (1..10).WHAT
(Range)
```

## Arrays
```{r}
> my @odd_digits = 1, 3, 5, 7, 9;
[1 3 5 7 9]
> my @single_digit_numbers = 0..9;
[0 1 2 3 4 5 6 7 8 9]
> my @weekdays = <mon tue wed thu fri>;
[mon tue wed thu fri]
```

Arrays can be **heterogenous**.
```{r}
> my @heterogenous-array = 1, 2.3, pi, "str", (1, 2, 4);
[1 2.3 3.14159265358979 str (1 2 4)]
```

Unassigned arrays are empty arrays.
```{r}
> my @empty;
[]
> my @empty = (); # Explicitly declared empty arrays
[]
```

Arrays are accessed using `@array[i]` syntax.
```{r}
> say "The third day is @weekdays[2]";
The third day is wed
> my @even-digits = 0, 2, 4, 6, 8;
[0, 2, 4, 6, 8]
> my @small-even_digits = @even-digits[0..2]
[0 2 4]
> my @min-max-even-digits = @even-digits[0, 4]
[0 8]
```

Arrays will expand if the index of assignment correspond to items that does not exist.
```{r}
my @arrays = 1, 2, 3;
@arrays[7] = 9;
say @arrays;
# [1 2 3 (Any) (Any) (Any) (Any) 9]
```

## Fixed-Sized, Typed, and Shaped
```{r}
my Int @numbers = 1..20;
my @months[12] = 1..7; # [1 2 3 4 5 6 7 (Any) (Any) (Any) (Any) (Any)]

# Multi-dimensional
my @employees
@employees[0;0] = "Liz";
@employees[0;1] = 3000;
@employess[1] = ["Bob"; 2500];
```


### Useful Methods

#### Adding and Removing Elements
1. `shift` Removes and returns first elements.
2. `pop` Removes and returns last elements.
3. `unshift` Adds an item or a list of items to the beginning of array.
4. `push` Adds an item or a list of items to the end of an array

_`push` and `unshift` **does not** flatten the elements inserted by default._

Use `|` prefix operator to flatten the added array.
```{r}
> my @numbers = <2 4 6 7>;
[2 4 6 7]
> @numbers.push(<8 10>);
[2 4 6 7 (8 10)]
> @numbers.push(|<8 10>);
[2 4 6 7 (8 10) 8 10]
```

The `:delete` adverb can be used to delete(and return) any elements in an array.
The deleted elements are replaced with undefined item `(Any)`.
```{r}
my @fruit = <apple banana pear cherry pineapple orange>;
my $removed = @fruit[2]:delete;
say $removed; # pear
say @fruit; [apple banana (Any) cherry pineapple orange]
```


#### Array Length
```{r}
> say @heterogenous-array.elems; # Return Array length
5
```

#### Identical Arrays
```{r}
# eqv
# To compare structure, type and value
my @even = 0, 2, 4, 6, 8;
my @even2 = reverse 8, 6, 4, 2, 0;
say @even eqv @even2; # True

# Smart match ~~
# To compare content only
say <1 2 3 4 5> eqv 1..5; # False
say <1 2 3 4 5> ~~ 1..5; #True

# Numeric equality ==
# Compare array length; == coerces arguments to numeric type
say <1 2 3 4 5> == <5 6 7 8 9>; # True
```

#### Reverse Slicing
```{r}
my @reverse-small-even_digits = @even-digits[reverse 0..2];
my @reverse-small-even_digits = reverse @even-digits[0..2];
```

#### Sorting Arrays or Lists

Default sort, is the smart sort, which use internal UNICODE representation to sort the data.
```{r}
say sort <4 6 2 9 1 5 11>;
```

The `sort` routine take 2 arguments, a code object and a list of item. If no code object is given, `cmp` is used.
1. `leg` - lexicographic comparisons
2. `<=>` - coerces to numeric and does numeric sort
3. `cmp` - compare string with string semantics and numbers with number semantics
```{r}
# Default/ cmp
say sort <4 6 2 9 1 5 11>;
say sort {$^a cmp $^b}, <4 6 2 9 1 5 11>;

# Reverse sort
say sort {$^b cmp $^a}, <4 6 2 9 1 5 11>;
say reverse sort <4 6 2 9 1 5 11>;

# Custom sort
say sort {$^a.lc leg $^b.lc}, <a ab abc A bc BAC AC>;

# To sort data of leading digits followed by letters and other characters,
# sorted by the letters
sub my_comp($str1,$str2){
  my $cmp1 = $0 if $str1 ~~ /\d+(\w+)/;
  my $cmp2 = $0 if $str1 ~~ /\d+(\w+)/;
  return $cmp1 cmp $cmp2;
}

say sort &my_comp, <22ac 34bd 56aa3 12c; 4abc( 1ca 45bc;
# (56aa3 4abc( 22ac 45bc 34bd 12c; 1ca)
```

`sort` code block with 1 parameters. The code block will not do the comparison, instead implementing transformation before using the default `cmp`.

Transformed values are **cached**, so the transformation is done only once for each item
```{r}
say sort {$_.lc}, <a ab abc A bc BAC AC>;
say sort {$_.abs}, <4 -2 5 3 -12 42 8 -7>;
```

#### Splicing
`splice` can add, remove, and return one or several items to or from an array.
```{r}
my @out_array = splice @array, $start, $num_elems, @replacement; 
# @replacement is optional
```

#### Traversing List
##### `for` loop

When traversion, `$_` is an alias to the items itself. Changing it will reflect in the array.
```{r}
my @colors = <red orange yellow green blue indigo violet>;
for @colors -> $color {
  say $color;
}
```

##### Read and re-assign elements (doubly pointy block)

Works on array but not list (immutable)
```{r}
my @colors = <red orange yellow green blue indigo violet>;
for @colors <-> $color {
  $color = tc $color; # turn every element Title Case
}
```

Using topical variable `$_`
```{r}
for colors {
  $_ = $_.uc
}
```

##### By index
```{r}
for 0..@colors.end -> $idx {
  say "$idx @colors[$idx]";
}
```

##### By Keys

`keys` will return a list of indices for the array or the list
```{r}
for keys @colors -> $idx {
  say "$idx @colors[$idx]";
}
```

##### By Keys-Values
`kv` returns an interleaved sequence of indexes and values
```{r}
for @letters.kv -> $idx, $val {
  say "$idx, $val";
}

my @letters = 'a'..'e';
say @letters.kv; # (0 a 1 b 2 c 3 d 4 e)
```

##### `loop`
C-like loop
```{r}
loop (my $i = 0; $i < 5; $i++){
  say $i, "->" ~ @letters[$i];
}
```

Infinite Loop
```{r}
loop{
  # do something
  #last if...
}
```

#### Unique Elements
```{r}
> say <a b d c a f d g>.unique;
(a b d c f g)
```

Use `squish` if the array is known to be **sorted**.

### Placeholders

`^` is a twigil, which state that the variable is a **placeholder parameter** or a **self declared positional parameter**. The variables are associated to the input according to their **lexicographic order**.
```{r}
my @letters = 'a'..'e';
for @letters.kv{
  say "$^a -> $^b"; # 'a' comes before 'b' hence first input is given to $a
}
```

## Reduce

An operation that combines a sequence of elements into a single value is called a reduction operation because its effect is to reduce all the items into one element.
```{r}
my $sum = reduce {$^a + $^b}, 1..10;
my $fact10 = reduce {$^a * $^b}, 1..10;
```

### Reduction Metaoperator

The `[...]` metaoperator iteratively applies the operator to all the values of the list to produce a single value.
```{r}
say [+] 1, 2, 3, 4; # 10
say [\+] 1, 2, 3, 4; # (1 3 6 10)

sub fact(Int $x){
  [*] 1..$x;
}

say [<] 3, 5, 7, 6; # False
say [lt] <a c d f r t y>; #True
```

## Map

Map "maps" a function to each of the elements in a sequence.

The `map` function iteratively assigns each item to `$_`, applies the code block to `$_` in order to create new values, and return a list of these new values.
```{r}
my @lc_words = <one tow three>;
my @all_caps = map {.uc}, @lc_words; # [ONE TWO THREE]
my @all_caps = map -> $word {$word.uc}, @lc_words; # [ONE TWO THREE]
my @all_caps = map {$^a.uc}, @lc_words; # [ONE TWO THREE]
```

The first argument to `map` can be a code reference
```{r}
sub double-sq-root-plus-one (Numeric $x){
  1 + 2 * sqrt $x;
}

my @results = map &double-sq-root-plus-one, 4, 16, 42;
```

## Filter

It selects some of the elements and filters out the other.

The `grep` function iteratively assigns each item to `$_` and applies code block following the `grep` keyword to `$_`, and returns a list of the values for which the code block evaluates to true.
```{r}
my @filtered = grep {/<[aeiouy]>/}, @input;
my @filtered = grep &contains-vowel, @input; # Can be used with code reference.
my @evens =  grep {$_ %% 2}, 1..10;
```

## Stacks and Queues

1. `stack` is a last in/ first out (LIFO) data structure
2. `queue` is a first in/ first out (FIFO) data structure
